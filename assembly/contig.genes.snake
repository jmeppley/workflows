"""
Rules that predict genes on a set of contigs using prodigal
"""
from python.common import get_file_name, apply_defaults
from python.assembly import filter_and_extract_rRNA, \
                            get_annotation_locations, \
                            drop_rna_overlaps, filter_annotations
# get gff parsing from py-metagenomics
from snakemake import logger

apply_defaults(config, {'prodigal': {'options': '-p meta -c'}})

# need rRNA hits to filter on
include: "../annotation/cmsearch.snake"

rule prodigal:
    """ get gene predictions form prodigal (defaults above to -p meta -c) """
    input:
        "contigs.all.fasta"
    output:
        gff="contigs.all.prodigal.genes.gff",
        faa="contigs.all.prodigal.genes.faa",
        ffn="contigs.all.prodigal.genes.ffn"
    benchmark:
        "benchmarks/contigs.all.prodigal.genes.time"
    version:
        get_version('prodigal','-v')
    params:
        opts=config['prodigal']['options']
    shell:
        "prodigal -i {input} -f gff -q {params.opts} \
         -o {output.gff} -a {output.faa} -d {output.ffn}"

rule rna_annotation:
    input:
        'contigs.all.vs.{mol}.cmsearch.gff',
        'contigs.all.fasta'
    output:
        fna='contigs.all.annotations.{mol}.fna',
        gff='contigs.all.annotations.{mol}.gff' ,
    benchmark: 'benchmarks/contigs.all.annotations.{mol}.time'
    params:
        buffer=config.get('max_annot_overlap', 0)
    run:
        filter_and_extract_rRNA(input[0], input[1],
                                get_file_name(output.fna),
                                get_file_name(output.gff),
                                wildcards.mol,
                                buffer=params.buffer)

rule drop_rna_overlaps:
    """
    Drop genes that overlap RNA predictions.
    """
    input:
        genes=expand("contigs.all.prodigal.genes.{ext}", \
                     ext=['faa', 'ffn', 'gff']),
        rna=expand('contigs.all.annotations.{mol}.gff', \
                    mol=['rRNA', 'tRNA']),
    output:
        expand('contigs.all.annotations.{suff}', \
                          suff=['CDS.gff', 'ffn', 'faa']),
    benchmark:
        "benchmarks/contigs.annotations.drop_rna_ovrlp.time"
    params:
        buffer=config.get('max_annot_overlap', 0)
    run:
        # get dict of rRNA locations
        rna_locations = get_annotation_locations(input.rna)

        # filter files
        for input_file in input.genes:
            print("Filtering {}".format(input_file))
            output_file = [f for f in output \
                                if f.endswith(input_file[-4:])][0]
            drop_rna_overlaps(input_file, output_file, rna_locations,
                              buffer=params.buffer)


rule filter_annotations:
    """
    pull out only annotations of good contigs
    """
    input:
        filter='contigs.filter.list',
        annots='contigs.all.annotations.{suff}'
    output: 'contigs.filter.annotations.{suff}'
    benchmark: 'benchmarks/contigs.filter.annotations.{suff}.time'
    wildcard_constraints:
        suff=r'(CDS\.)?(gff|fna|faa|ffn)'
    run:
        # filter annotations
        filter_annotations(get_file_name(input.filter),
                           get_file_name(input.annots),
                           get_file_name(output))



rule merge_annotations:
    """
    combine protein coding genes (.ffn)
    with rRNA genes
    into .fna file

    This assumes:
     * ovelapping genes already removed from ffn
     * annots are sorted by contig number and start pos
     * contigs named: {assembly_name}_c{num}
    """
    input:
        genes='contigs.all.annotations.ffn',
        rna=expand('contigs.all.annotations.{mol}.fna', \
                   mol=['rRNA', 'tRNA'])
    output:
        faa='contigs.all.annotations.fna',
    benchmark: 'benchmarks/contigs.all.merged.fna.time'
    run:
        # Load all rRNA genes as giant list
        rna_genes = []
        for gf in input.rna:
            rna_genes.extend(rna for rna in SeqIO.parse(gf, 'fasta'))

        def get_next_rna():
            # Queue up next RNA to insert
            try:
                next_rna = rna_genes.pop(0)
            except IndexError:
                return None
            contig_num, start = \
                re.search(r'^\S+_c(\d+)_[rt]RNA_\d+\s.+start=(\d+);',
                          next_rna.description).groups()
            return (int(contig_num), int(start), next_rna)

        next_rna = get_next_rna()

        # loop over potein coding genes
        with open(get_file_name(output), 'wt') as OUT:
            for gene in SeqIO.parse(get_file_name(input.genes), 'fasta'):
                # get start pos of next gene
                contig_num, start = \
                    re.search(r'^\S+_(c\d+)_\d+\s+#\s*(\d+)\s*#',
                              gene.id).groups()
                gene_pos = (int(contig_num), int(start))

                # write out andy RNA that come before it
                while next_rna is not None and next_rna[:2] < gene_pos:
                    OUT.write(next_rna[-1].format('fasta'))
                    next_rna = get_next_rna()

                # write out gene
                OUT.write(gene.format('fasta'))

            # write out any remaining RNA
            while next_rna is not None:
                OUT.write(next_rna[-1].format('fasta'))
                next_rna = get_next_rna()

rule merge_annotation_gff:
    """ put filtered RNA and protein coding genes into one GFF file """
    input:
        genes='contigs.all.annotations.CDS.gff',
        rna=expand('contigs.all.annotations.{mol}.gff', mol=['rRNA', 'tRNA'])
    output:
        faa='contigs.all.annotations.gff',
    benchmark: 'benchmarks/contigs.all.merged.gff.time'
    shell:
        "cat {input.rna} {input.genes} | grep -v '^#' | sort > {output}"
