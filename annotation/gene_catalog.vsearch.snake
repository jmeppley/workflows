from python.common import apply_defaults
from python.gene_catalog import parse_bio_clusters

# software parameters
defaults = {
    'vsearch': {
        'threads': 20,
        'options': '--id 0.95 --iddef 0 --strand plus --minsl 0.6',
        'output_str': 'vsearch.id.95.minsl.6',
    },
}

apply_defaults(config, defaults)
cluster_suffix = config['vsearch']['output_str']

rule cluster_ffn:
    input: "all_genes.ffn"
    output:
        ffn="all_genes.ffn.{}".format(cluster_suffix),
        bio="all_genes.ffn.{}.bio".format(cluster_suffix),
    benchmark: 'benchmarks/all_genes.ffn.{}.time' \
                        .format(cluster_suffix)
    threads: config['vsearch']['threads']
    shell:
        "vsearch --cluster_fast {input} --no_progress \
         --biomout {output.bio} \
         {config[vsearch][options]} \
         --threads {threads} --centroids {output.ffn}"

rule parse_clusters:
    """ turn .clstr file into simple cluster table """
    input: "all_genes.ffn.{}.bio".format(cluster_suffix)
    output: prefix + ".clusters"
    benchmark: 'benchmarks/{}.clusters.time'.format(prefix)
    run:
        parse_bio_clusters(get_file_name(input),
                           get_file_name(output))
