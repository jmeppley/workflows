import tempfile
from python.common import apply_defaults

# software parameters
defaults = {
    'mmseqs2': {
        'threads': 20,
        'program': 'cluster',
        'options': '--min-seq-id 0.97 -c 0.8 --cov-mode 0 --cluster-mode 0',
        'output_str': 'cluster.setcover.97',
        'tmp_dir_root': '.',
    },
}
apply_defaults(config, defaults)
cluster_suffix = config['mmseqs2']['output_str']
config['cluster_type'] = 'faa'
tmp_dir = tempfile.TemporaryDirectory(dir=config['mmseqs2']['tmp_dir_root'],
                                      prefix='tmp_mmseqs2_')

rule extract_faa:
    input: 
        reps="all_genes.faa.{}.reps".format(cluster_suffix),
        db="all_genes.faa"
    output: "all_genes.faa.{}".format(cluster_suffix)
    benchmark: 'benchmarks/all_genes.faa.{}.extract.faa.time' \
                        .format(cluster_suffix)
    shell:
        """
         mmseqs result2flat {input.db} {input.db} {input.reps} {output} \
           --use-fasta-header > {output}.log
        """

rule extract_reps:
    input:
        clusters="all_genes.faa.{}.result".format(cluster_suffix),
        db='all_genes.faa',
    output: "all_genes.faa.{}.reps".format(cluster_suffix)
    benchmark: 'benchmarks/all_genes.faa.{}.extract.reps.time' \
                        .format(cluster_suffix)
    shell:
        """
         mmseqs result2repseq {input.db} {input.clusters} {output} > {output}.log
        """

rule cluster_faa:
    input: "all_genes.faa.dbtype"
    output: "all_genes.faa.{}.result".format(cluster_suffix)
    benchmark: 'benchmarks/all_genes.faa.{}.cluster.time' \
                        .format(cluster_suffix)
    threads: config['mmseqs2']['threads']
    params:
        input='all_genes.faa',
        tmp_dir=tmp_dir.name
    shell:
        "mmseqs {config[mmseqs2][program]} \
            {params.input} {output} {params.tmp_dir} \
            {config[mmseqs2][options]} --threads {threads}"

rule formatdb_for_mmseqs:
    input: "all_genes.faa"
    output: 'all_genes.faa.dbtype'
    benchmark: 'benchmarks/format_for_mmseqs.time'
    shell: "mmseqs createdb {input} {input} > {input}.createdb.log 2>&1"
