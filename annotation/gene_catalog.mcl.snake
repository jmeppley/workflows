from python.common import get_version, apply_defaults, get_file_name
from python.gene_catalog import process_for_mcl

# software parameters
defaults = {
    'mcl': {
        'threads': 20,
        'options': '-I 2',
        'filters': {'pctid': .95, 'minbit': .5},
        'output_str': 'mcl.I2',
        'use_last8': True,
        'last_opts': "",
    },
}
apply_defaults(config, defaults)
cluster_suffix = config['mcl']['output_str']

rule mcl:
    input: "all_genes.v.self.pctid.tab"
    output: "all_genes.ffn.mcl.raw"
    benchmark: "benchmarks/mcl.time"
    threads: config['mcl']['threads']
    shell: "mcl {input} --abc {config[mcl][options]} \
            -o {output} -te {threads}"

rule get_reps:
    input:
        cluster=rules.mcl.output,
        faa='all_genes.ffn',
    output: 'all_genes.ffn.{}'.format(cluster_suffix)
    benchmark: "benchmarks/get_reps.time"
    shell: "screen_list.py {input.faa} -k -l <(cut -f 1 {input.cluster}) \
            -o {output}"

rule all_v_all_self_db:
    input: "all_genes.ffn"
    output: "all_genes.ffn.prj"
    benchmark: 'benchmarks/all_genes.ffn.v.self.db.time'
    threads: config['mcl']['threads']
    params:
        exe='lastdb8' if config['mcl']['use_last8'] else 'lastdb',
    shell:
        """
        {params.exe} -P {threads} {input} {input}
        """

rule all_v_all_self:
    input:
        "all_genes.ffn",
        "all_genes.ffn.prj",
    output: "all_genes.ffn.v.self.last"
    benchmark: 'benchmarks/all_genes.ffn.v.self.time'
    threads: config['mcl']['threads']
    params:
        exe='lastal8' if config['mcl']['use_last8'] else 'lastal',
    shell:
        """
        {params.exe} -P {threads} -f BlastTab {config[mcl][last_opts]} \
         {input[0]} {input[0]} \
         > {output}
        """

rule process_for_mcl:
    input:
        hits=rules.all_v_all_self.output,
        fasta='all_genes.ffn',
    output: "all_genes.v.self.pctid.tab"
    benchmark: "benchmarks/process_for_mcl.time"
    run:
        process_for_mcl(get_file_name(input.hits),
                        get_file_name(input.fasta),
                        get_file_name(output),
                        format='blast',
                        pctid=config['mcl']['filters']['pctid'],
                        minbit=config['mcl']['filters']['minbit'],)
