from python.gene_catalog import parse_cdhit_clusters

# software parameters
defaults = {
    'cdhit': {
        'threads': 20,
        'options': '-c 0.95 -M 1000000 -G 0 -aS 0.9 -g 1 -r 1 -d 0',
        'output_str': 'c.95.aS.9.g1.r1.G0',
    },
}
apply_defaults(config, defaults)
cluster_suffix = config['cdhit']['output_str']
CLUSTER_REPS = f"all_genes.ffn.{cluster_suffix}"

rule cluster_ffn:
    input: "all_genes.ffn"
    output:
        ffn=CLUSTER_REPS,
        clstr=CLUSTER_REPS + ".clstr"
    benchmark: 'benchmarks/all_genes.ffn.{}.time' \
                        .format(cluster_suffix)
    threads: config['cdhit']['threads']
    params:
        cd_hit_options=config['cdhit']['options']
    shell:
        "cd-hit-est -T {threads} {params.cd_hit_options} \
         -i {input} -o {output.ffn}"

rule parse_clusters:
    """ turn .clstr file into simple cluster table """
    input: "all_genes.ffn.{}.clstr".format(cluster_suffix)
    output: prefix + ".clusters"
    benchmark: 'benchmarks/{}.clusters.time'.format(prefix)
    run:
        parse_cdhit_clusters(get_file_name(input),
                             get_file_name(output))
