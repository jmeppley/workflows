rule get_fasta_id_map:
    """
    pull out fasta headers into a map from ID to Description
    """
    input: '{file_root}.{fasta_ext}'
    output: "{file_root}.{fasta_ext}.headers"
    wildcard_constraints:
        fasta_ext='(faa|ffn|fna|fasta|fa)',
    resources:
        disk=1
    shell: r"grep '^>' {input} | sed -r 's/^>(\S+)\s+/\1\t/' > {output}"

rule link_fasta_id_map:
    """
    Create smlink to headers from formatted db loc
    """
    input: '{file_root}.{fasta_ext}.headers'
    output: "{file_root}.{fasta_ext}.{db_string}.ids"
    wildcard_constraints:
        fasta_ext='(faa|ffn|fna|fasta|fa)',
        db_string='(bwa/bwadb|ldb8?/lastdb|dmnd)'
    shell:
        "ln -s $(realpath --relative-to=$(dirname {output}) {input}) {output}"

rule link_tax_id_map:
    """
    Create smlink to taxid map from formatted db loc
    """
    input: '{file_root}.{fasta_ext}.tax'
    output: "{file_root}.{fasta_ext}.{db_string}.tax"
    wildcard_constraints:
        fasta_ext='(faa|ffn|fna|fasta|fa)',
        db_string='(bwa/bwadb|ldb8?/lastdb|dmnd)'
    shell:
        "ln -s $(realpath --relative-to=$(dirname {output}) {input}) {output}"

