include: "../common/tool.path.snake"
from edl import __version__ as PYMG_VERSION
SAMTOOLS_VERSION = get_version('samtools', lines=0)
BWA_VERSION = \
        get_version('bwa', 
                    version_flag="", 
                    regular_expression=re.compile(r'Version:\s*(\S[^\n\r]+\S)'))
from snakemake.utils import update_config
update_config(config, {'bwa': {'threads': 15}})
config['cov_col'] = 'md cov'

#######
# map reads to contigs with BWA:
#  4 rules

rule index_contigs:
    """ prepare contigs for mapping with BWA """
    input:
        "contigs.fasta"
    output:
        "mapping/contigs.index.bwa.bwt"
    log:
        "logs/bwa_index.log"
    benchmark:
        "benchmarks/bwa_pre_index.time"
    version: BWA_VERSION
    shell:
        "bwa index -p mapping/contigs.index.bwa {input} > {log} 2>&1 "

def get_sample_reads(wildcards):
    """
    reads for mapping can come from different places
    """
    sample = wildcards.sample
    if sample in config['sample_data']:
        # look in configuraation first
        return config['sample_data'][wildcards.sample]['raw'] \
            if 'raw' in config['sample_data'][wildcards.sample] \
            else config['sample_data'][wildcards.sample]['clean']
    else:
        # otherwise, look for fastq file
        return "{}.fastq".format(wildcards.sample)

rule map_sample_reads:
    """
    map cleaned reads onto contigs
    """
    input:
        "mapping/contigs.index.bwa.bwt",
        # prefer the raw reads, but cleaned are OK
        get_sample_reads
    output:
        temp("mapping/{sample}.reads.vs.contigs.sam")
    log:
        "logs/{sample}.vs.contigs.bwa_mem.log"
    benchmark:
        "benchmarks/{sample}.vs.contigs.bwa_mem.time"
    version: BWA_VERSION
    threads: config['bwa']['threads']
    shell:
        "bwa mem -p -t {threads} mapping/contigs.index.bwa {input[1]} \
            2> {log} \
            > {output}"


rule sam_to_bam:
    input: "{root}.sam"
    output: "{root}.bam"
    benchmark: "benchmarks/{root}.bwa_sam_bam.time"
    threads: 1
    version: SAMTOOLS_VERSION
    shell:
        "samtools view -b {input} \
        | samtools sort -l 5 -@ {threads} -m 10G - \
        > {output}"

rule index_bam:
    input: "{root}.bam"
    output: "{root}.bam.bai"
    benchmark: "benchmarks/{root}.bwa_index_bam.time"
    version: SAMTOOLS_VERSION
    shell:
        "samtools index {input}"

rule mapped_read_counts:
    """ 
    Count number of reads mapped to each contig from a sample. 
    
    This just pulls out the refernce column (3) from the SAM output
    and uses uniq -c to get a count.
    """
    input:
        "mapping/{sample}.vs.contigs.bam.bai"
    output:
        "mapping/{sample}.vs.contigs.read_counts"
    benchmark:
        "benchmarks/bwa_count_reads.{sample}.time"
    version:
        get_version('samtools', lines=0)
    params:
        input="mapping/{sample}.vs.contigs.bam"
    shell:
        "samtools view {params.input} \
         | cut -f 3 \
         | uniq -c \
         > {output}"

rule mapped_read_depths:
    """
    Get read depth at each base in each contig. This is needed for
    coverage calculation later.
    """
    input:
        "mapping/{sample}.vs.contigs.bam.bai"
    output:
        "mapping/{sample}.vs.contigs.depths"
    benchmark:
        "benchmarks/bwa_get_depths.{sample}.time"
    version:
        get_version('samtools', lines=0)
    params:
        input="mapping/{sample}.vs.contigs.bam"
    shell:
        "samtools depth {params.input} > {output}"

rule contig_stats_table:
    """
    Combine stats from fasta header with depths and read counts to 
    make a master table of contig stats
    """
    input:
        "contigs.fasta",
        "mapping/{sample}.reads.vs.contigs.depths",
        "mapping/{sample}.reads.vs.contigs.read_counts",
    output:
        "contigs.coverages.{sample}.txt",
        "contigs.histograms.{sample}.txt"
    benchmark:
        "benchmarks/contig.stats.{sample}.time"
    version:
        "py-metagenomics-{}".format(PYMG_VERSION)
    shell:
        "python {config[pymg_dir]}/edl/assembly.py get_contig_stats {input} {output} txt_width=75 log=True"

