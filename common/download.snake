"""
rules and code to automatically get remote files

A combination of snakemake's SFTP remote and rsync is used to get files:
    * SFTP used for wildcard globs
    * rsync used for download (comes with checksum verification!)

This loses snakemake's ability to notice changes to remote files. :(

Currently, only supports password-less ssh (consider using ssh-agent!).
Configure user name in the configuration. EG:
config:
    remote:
        SFTP:
            defaults:
                username: usualid
            other.host.edu:
                username: otherid

if you include this workflow and pass file names through remote_wrapper()
snakemake will download file to local copy first
"""
from python.download import remote_wrapper

download_map = config.setdefault('download_map', {})

rule download_remote:
    input: lambda w: download_map[w.dl_path]['remote']
    output: temp("remote/{dl_path}")
    params:
        link=lambda w: ("$(realpath --relative-to=$(dirname remote/{}) {})"
                        .format("w.dl_path",
                                download_map[w.dl_path]['remote']))
    shell: "rm -f {output} && ln -s {params.link} {output}"
    
rule download_rsync:
    output: temp("rsync/{dl_path}")
    params:
        host=lambda w: download_map[w.dl_path]['host'],
        remote_path=lambda w: download_map[w.dl_path]['remote_path'],
        user=lambda w: download_map[w.dl_path]['user'],
        dl_path=lambda w: os.path.dirname("rsync/" + w.dl_path)
    shell: """
            mkdir -p {params.dl_path}
            rsync -a {params.user}@{params.host}:{params.remote_path} {params.dl_path}
           """
