
# adds fastq stats files to output
# (only runs if config[discover_fastx_for_stats] is set to True)
# this sets up a dry run of the workflow, 
#  finds fasta and fastq outputs,
#  adds stats/hist files to outputs of this workflow
# (It adds some serious startup time)
from python.common import get_version, add_stats_outputs
logger.debug("INIT STATS")
add_stats_outputs(workflow.snakefile, config) 

try:
    hist_version = get_version('histograms.py')
    try:
        logger.debug("Histogram version: {}".format(hist_version))
    except:
        pass
    enable_hist = True
except:
    enable_hist = False

if enable_hist:
    rule histogram_simple:
        """ produce a histogram of sequence lengths """
        input:
            "{file_root}.{ext}"
        output:
            "stats/{file_root}.{ext,f(aa|fn|na|a|asta|astq)(\.gz)?}.hist"
        resources:
            disk=1
        log:
            "logs/{file_root}.{ext}.hist.log"
        benchmark:
            "benchmarks/{file_root}.{ext}.hist.time"
        version:
            hist_version
        params:
            cat=lambda wildcards:"gunzip -c" \
             if re.search(r'\.gz$',wildcards.ext) else "cat"
        shell:
            """
            {params.cat} {input} | \
            seqtk comp | \
            cut -f 2 | \
            histograms.py -l length -b 30 > {output}
            """

else:
    rule histogram_dummy:
        """ create a placeholder file so that the workflow is happy """
        input:
            "{file_root}.{ext}"
        output:
            "stats/{file_root}.{ext,f(aa|fn|na|a|asta|astq)(\.gz)?}.hist"
        shell:
            "echo 'histograms.py is not installed' > {output}"

rule prinseq_any:
    """ calculate basic stats for a fasta file """
    input:
        "{file_root}.{ext}"
    output:
        "stats/{file_root}.{ext,f[a-z]+(\.gz)?}.stats"
    resources:
        disk=1
    log:
        "logs/{file_root}.{ext}.stats.log"
    benchmark:
        "benchmarks/{file_root}.{ext}.stats.time"
    params:
        input_flag=lambda wildcards:"-fastq" \
         if re.search(r'q',wildcards.ext) else "-fasta",
        aa=lambda wildcards:"-aa" \
         if re.search(r'\bfaa\b',wildcards.ext) else "",
        cat=lambda wildcards:"gunzip -c" \
         if re.search(r'\.gz$',wildcards.ext) else "cat"
    version:
        get_version('prinseq-lite.pl','-version')
    shell:
        """
        LINES=$({params.cat} {input} | wc -l)
        if [ "$LINES" -gt "0" ]; then 
          {params.cat} {input} | \
          prinseq-lite.pl {params.input_flag} stdin \
           -stats_len -stats_info {params.aa} > {output}
        else
          echo -e "stats_info\tbases\t0\nstats_info\treads\t0" > {output}
        fi
        """


