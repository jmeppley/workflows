"""
Workflow for assembling transcriptomic samples

 * renames reads to sample names
 * optionally cleans with:
   - bbduk (adapters)
   - bfc (error correction)
   - trimmomatic (quality and length)
 * runs assembler (SPAdes only, for now)
 * renames transcripts
    (to config[assembly_name], if given)
    (to sample name, if only one)
 * separates into rRNA and transcripts

####
# Required conda packages:
#
# snakemake bbmap infernal spades prodigal pandas biopython bfc (optional)
###

see test/data/configs/spades.yaml for an example configuration

"""
import os, re, sys, glob, yaml, pandas, subprocess, tempfile
from Bio import SeqIO, __version__ as BIOPYTHON_VERSION
from python.common import get_version, parse_stats
from python.qc import setup_qc_outputs

# add pymg to path
include: "common/tool.path.snake"

# Initialize transitions
config.setdefault('transitions', {})

##
# Bring in components
#include: "common/common.snake"
include: "common/stats.snake"
#
# for mapping reads back to contigs
include: "common/mapping_bwa.snake"
#
## set up any QC
# don't separate rRNA in reads
config.setdefault('remove_rna', False)
# use the assembly cleaning protocol
config.setdefault('cleaning_protocol', 'assembly')
outputs = config.setdefault('outputs',set())
cleaned_reads = setup_qc_outputs(config)
outputs.update(cleaned_reads)
include: "qc/setup.snake"
#
# the assembler (add link from assembled contigs to ./contigs.raw.fasta)
assembler = config.get('assembler', 'spades')
if assembler == 'spades':
    include: "assembly/metagenomic.spades.snake"
    config['transitions']['contigs.raw.fasta'] = 'spades/transcripts.fasta'
elif assembler == 'megahit':
    raise Excption("Unsupported RNA assembler: " + config['assembler'])
else:
    raise Excption("Unknown assembler: " + config['assembler'])
#
# contig annotation (RNA, genes, coverage, stats)
include: "assembly/contig.annotation.snake"
#
# separate rRNA from transcripts at end
include: "qc/sort.rna.snake"
outputs.add('contigs.non-rRNA.fasta')
outputs.add('contigs.rRNA-only.fasta')
#
# rule to create links
include: "common/transitions.snake"

# make sure tanstion source files don't get deleted
config['outputs'].update(config['transitions'].values())

# 
# Make sure assembly has name
if 'assembly_name' not in config:
    # if there is only one sample, use that
    sample_names = [sample \
                         for sample,data in config['sample_data'].items() \
                         if 'clean' in data]
    if len(sample_names)==1:
        config['assembly_name'] = sample_names[0]
    else:
        raise Exception("Please supply a naming string for this assembly "
                        "in config[assembly_name].")

logger.debug(config)

############
# RULES
#
# Start with the final product(s):
rule all:
    input:
        config['outputs'],
        "assembly.{}.report".format(config['assembly_name']),

rule contigs_fasta:
    """
    Rename contigs
    """
    input:
        "spades/contigs.fasta"
    output:
        "contigs.fasta"
    log:
        "logs/contigs.fasta.link.log"
    benchmark:
        "benchmarks/contigs.fasta.txt"
    version:
        "biopython-{}".format(BIOPYTHON_VERSION)
    run:
        root_name = config['assembly_name']
        with open(input[0]) as INPUT:
            with open(output[0], 'w') as OUTPUT:
                for i, (title, sequence) \
                    in enumerate(SeqIO.FastaIO.SimpleFastaParser(INPUT)):
                        contig_name = "%s_c%d" % (root_name, i+1)
                        OUTPUT.write(">%s %s\n%s\n" % (contig_name,
                                                       title,
                                                       sequence))

def get_read_files(reads_type, config):
    for sample, sample_data in config['sample_data'].items():
        if reads_type in sample_data:
            reads_files = sample_data[reads_type]
            if isinstance(reads_files, str):
                yield reads_files
            else:
                for reads_file in reads_files:
                    yield reads_file

rule final_report:
    input:
        #raw_stats=['stats/{}.stats'.format(reads_file) \
        #           for reads_file in get_read_files('raw', config)],
        cleaned_stats=['stats/{}.stats'.format(reads_file) \
                   for reads_file in get_read_files('clean', config)],
        ssu_rrnas="contigs.vs.rRNA.cmsearch.SSU.gt1200.tsv",
        lsu_rrnas="contigs.vs.rRNA.cmsearch.LSU.gt2000.tsv",
        gene_stats="stats/contigs.annotations.faa.stats",
        contig_stats="stats/contigs.fasta.stats",
        histograms=expand("contigs.histograms.{sample}.txt",
                     sample=[s for s in config['sample_data'] \
                               if 'clean' in config['sample_data'][s]])
    output:
        "assembly.{assembly_name}.report"
    run:
        read_stats={}

        # paired reads
        for sample, sample_data in config['sample_data'].items():
            #for reads_type in ['raw', 'clean']:
            for reads_type in ['clean']:
                if reads_type not in sample_data:
                    continue
                reads_files = sample_data[reads_type]
                if isinstance(reads_files, str):
                    reads_files = [reads_files,]
                for reads_file in reads_files:
                    stats_file = "stats/{}.stats".format(reads_file)
                    stats = parse_stats(stats_file)
                    match = re.search(r'R[12]', stats_file)
                    if match:
                        key_pref = sample + " " + reads_type + " " + match.group()
                    else:
                        key_pref = sample + " " + reads_type
            
                    read_stats['{} reads'.format(key_pref)]=stats['reads']
                    read_stats['{} bases'.format(key_pref)]=stats['bases']

        # contigs
        contig_stats={}
        stats = parse_stats(input.contig_stats)
        contig_stats['contigs']=stats['reads']
        contig_stats['contig bases']=stats['bases']

        # N##
        stats=pandas.read_table(input.histograms[0],
                                sep=':',
                                index_col='key', 
                                names=('key','value'),
                                nrows=8)
        stats=stats.set_index(stats.index.str.strip())['value']
        contig_stats['N50']=int(stats['N50'])
        contig_stats['N75']=int(stats['N75'])
        contig_stats['N90']=int(stats['N90'])

        # annotations
        stats = parse_stats(input.gene_stats)
        contig_stats['genes']=stats['reads']

        # full length SSUs
        ssu_count=-1
        with open(input.ssu_rrnas) as INF:
            for line in INF:
                ssu_count+=1
        contig_stats['SSUs']=ssu_count

        # full length LSUs
        lsu_count=-1
        with open(input.lsu_rrnas) as INF:
            for line in INF:
                lsu_count+=1
        contig_stats['LSUs']=lsu_count

        report={"reads":read_stats,
                "assembly":contig_stats,
                "assembly_name":wildcards.assembly_name}
        with open(output[0],'w') as OUTF:
            OUTF.write(yaml.dump(report, default_flow_style=False))

